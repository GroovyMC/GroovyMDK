plugins {
    id 'groovy'
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.groovymc.modsdotgroovy' version '1.4.1' // for mods.groovy
}

version = '1.20-1.0'
group = 'com.authorname.examplemod'

// make sure to change all instances of 'examplemod'/'ExampleMod' in the project folder - not just this one!
base {
    archivesName = 'examplemod' // your modId - must be lowercase and no spaces.
}

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: 'official', version: '1.20'

    //accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg') // Currently, this location cannot be changed from the default.

    // Default run configurations. Use the genIntellijRuns/genEclipseRuns task to generate these for your IDE.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        // applies to all run configs
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'

            mods {
                "${archivesBaseName}" {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', archivesBaseName
        }

        server {
            property 'forge.enabledGameTestNamespaces', archivesBaseName
            args '--nogui'
        }

        // This run config launches GameTestServer and runs all registered gametests, then exits.
        // By default, the server will crash when no gametests are provided.
        // The gametest system is also enabled by default for other run configs under the /test command.
        gameTestServer {
            property 'forge.enabledGameTestNamespaces', archivesBaseName
        }

        data {
            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', archivesBaseName, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

// Comment this out if you intend on having your mod contain both Groovy and Java code
sourceSets.each { sourceSet -> file("src/${sourceSet.name}/java/").deleteDir() }

repositories {
    maven {
        name = 'Modding Inquisition Releases'
        url = 'https://maven.moddinginquisition.org/releases'
    }
}

dependencies {
    // Forge
    minecraft 'net.minecraftforge:forge:1.20-46.0.14'

    // GroovyModLoader
    compileOnly 'org.groovymc.gml:gml:4.0.0'
    runtimeOnly('org.groovymc.gml:gml:4.0.0:all') {
        exclude module: 'transform'
    }

    // CommonGroovyLibrary (bundled with GML)
    compileOnly 'org.groovymc.cgl:cgl-1.20-forge:0.3.3'
}

tasks.named('jar', Jar) {
    manifest {
        attributes([
                'Specification-Title'   : archivesBaseName,
                'Specification-Vendor'  : 'authorName',
                'Specification-Version' : '1', // We are version 1 of ourselves
                'Implementation-Title'  : archivesBaseName,
                'Implementation-Version': archiveVersion,
                'Implementation-Vendor' : 'authorName',
        ])
    }

    // Helps builds be more reproducible and cacheable
    reproducibleFileOrder = true
    preserveFileTimestamps = false

    // This is the preferred method to re-obfuscate your jar file
    finalizedBy 'reobfJar'
}

// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
//tasks.named('publish') {
//    dependsOn 'reobfJar'
//}

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(GroovyCompile).configureEach {
    options.incremental = true
    groovyOptions.encoding = 'UTF-8' // Use the UTF-8 charset for Groovy compilation
    groovyOptions.optimizationOptions.indy = true
    groovyOptions.optimizationOptions.groovydoc = true
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

modsDotGroovy {
    dslVersion = '1.5.0'
}
